amends "@crossplane.contrib/CompositionResponse.pkl"
import "@crossplane.contrib/crossplane.pkl"

import ".../crds/XR.pkl"
import ".../crds/Object.pkl"

import "@k8s/api/core/v1/ConfigMap.pkl"

local request = new crossplane {
  customResourceTemplates = new {
    ["XR"] {
      ["example.crossplane.io/v1"] = XR
    }
    ["Object"] {
      ["kubernetes.crossplane.io/v1alpha2"] = Object
    }
  }
}.Request

local observedCompositeResource: XR? = request.observed.composite.resource as XR?
local extraResource: Object? = request.getExtraResource("ineed", 0)?.resource as Object?

requirements {
  extraResources {
    ["ineed"] {
      apiVersion = Object.apiVersion
      kind = Object.kind
      match {
        matchName = "required"
      }
    }
  }
}

desired {
  composite {
    resource = new XR {
      status {
        someStatus = "pretty status"
      }
    }
  }
  resources {
    ["cm-one"] = new {
      resource = new Object {
        spec {
          forProvider {
            manifest = new ConfigMap {
              metadata {
                namespace = "crossplane-system"
              }
              data {
                ["foo"] = observedCompositeResource?.metadata?.name ?? throw("Composite could not find observed composite name")
                ["required"] = extraResource?.metadata?.name ?? "i could not find what I needed..."
              }
            }
          }
        }
      }
      ready = true
    }
  }
}
results {
  new {
    severity = "Normal"
    message = "welcome"
  }
}
context {
  ["greetings"] = "with <3 from function-pkl"
  when (request.context.containsKey("apiextensions.crossplane.io/environment")) {
    ["apiextensions.crossplane.io/environment"] = request.context.getOrNull("apiextensions.crossplane.io/environment")
  }
}

meta = if (request.meta != null) new {
  ttl = 60.s
} else null

local observedObj: Object? = request.observed.resources.getOrNull("cm-one").resource as Object?
local observedObjSyncedCondition: Object.Condition? = observedObj.ifNonNull(
  (obj: Object) -> obj.status.conditions.toList().findOrNull(
    (cond: Object.Condition) -> cond.type == "Synced"
  )
)

conditions {
  if (observedObjSyncedCondition != null && observedObjSyncedCondition.message.contains("connect failed: cannot get provider config"))
    new {
      target = TARGET_COMPOSITE_AND_CLAIM
      status = STATUS_CONDITION_FALSE
      message = "The ProviderConfig 'default' is missing"
      reason = "ProviderConfig"
      type = "Healthy"
    }
  else
    new {
      target = TARGET_COMPOSITE_AND_CLAIM
      status = STATUS_CONDITION_TRUE
      reason = "TestsPassed"
      type = "Healthy"
    }
}
